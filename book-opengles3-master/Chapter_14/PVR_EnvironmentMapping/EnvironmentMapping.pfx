[HEADER]
	VERSION		00.00.00.00
	DESCRIPTION EnvironmentMapping
[/HEADER]

[TEXTURE]
	NAME 			baseMap 
	PATH			baseMap.pvr 
	MINIFICATION	LINEAR
	MAGNIFICATION	LINEAR
[/TEXTURE]

[TEXTURE]
	NAME 			bumpMap 
	PATH			bumpMap.pvr 
	MINIFICATION	LINEAR
	MAGNIFICATION	LINEAR
[/TEXTURE]

[TEXTURE]
	NAME 			envMap 
	PATH			envMap.pvr 
	MINIFICATION	LINEAR
	MAGNIFICATION	LINEAR
[/TEXTURE]

[VERTEXSHADER]
	NAME VertShader
	
	[GLSL_CODE]
#version 300
uniform mat4 u_matViewInverse;
uniform mat4 u_matViewProjection;
uniform vec3 u_lightPosition;

in vec4 a_vertex;
in vec2 a_texcoord0;
in vec3 a_normal;
in vec3 a_binormal;
in vec3 a_tangent;

out vec2 v_texcoord;
out vec3 v_lightDirection;
out vec3 v_normal;
out vec3 v_binormal;
out vec3 v_tangent;
   
void main( void )
{  
   // Transform light position into world space
   vec3 lightPositionWorld = (u_matViewInverse * vec4(u_lightPosition, 1.0)).xyz;   
   
   // Compute world-space light direction vector
   vec3 lightDirectionWorld = lightPositionWorld - a_vertex.xyz;
   
   // Pass the world-space light vector to the fragment shader
   v_lightDirection = lightDirectionWorld;
      
   // Transform output position
   gl_Position = u_matViewProjection * a_vertex;
   
   // Pass through other attributes
   v_texcoord = a_texcoord0.xy;
   v_normal = a_normal;
   v_binormal = a_binormal;
   v_tangent = a_tangent;

}
	[/GLSL_CODE]
[/VERTEXSHADER]

[FRAGMENTSHADER]
	NAME FragShader

	[GLSL_CODE]
#version 300
precision mediump float;

uniform vec4 u_ambient;
uniform vec4 u_specular;
uniform vec4 u_diffuse;
uniform float u_specularPower;

uniform sampler2D s_baseMap;
uniform sampler2D s_bumpMap;
uniform samplerCube s_envMap;


in vec2 v_texcoord;
in vec3 v_lightDirection;
in vec3 v_normal;
in vec3 v_binormal;
in vec3 v_tangent;

layout(location = 0) out vec4 fragColor;

void main( void )
{
   // Fetch basemap color
   vec4 baseColor = texture( s_baseMap, v_texcoord );
   
   // Fetch the tangent space normal from normal map
   vec3 normal = texture( s_bumpMap, v_texcoord ).xyz;
   
   // Scale and bias from [0, 1] to [-1, 1]
   normal = normal * 2.0 - 1.0;
   
   // Construct a matrix to transform from tangent to world space
   mat3 tangentToWorldMat = mat3( v_tangent, 
                                  v_binormal, 
                                  v_normal );
                                  
   // Transform normal to world space and normalize
   normal = normalize( tangentToWorldMat * normal );
   
   // Normalize the light direction 
   vec3 lightDirection = normalize( v_lightDirection );
   
   // Compute N.L
   float nDotL = dot( normal, lightDirection );
   
   // Compute reflection vector
   vec3 reflection = ( 2.0 * normal * nDotL ) - lightDirection;
   
   // Use the reflection vector to fetch from the environment map
   vec4 envColor = texture( s_envMap, reflection );
   
   // Output final color
   fragColor =  0.25 * baseColor + envColor;
}
	[/GLSL_CODE]
[/FRAGMENTSHADER]
 
[EFFECT] 
	NAME    DefaultEffect

	ATTRIBUTE a_vertex			POSITION
	ATTRIBUTE a_texcoord0		UV0
	ATTRIBUTE a_normal			NORMAL
	ATTRIBUTE a_tangent			TANGENT
	ATTRIBUTE a_binormal		BINORMAL

	UNIFORM u_matViewProjection	WORLDVIEWPROJECTION
	UNIFORM u_matViewInverse	WORLDVIEWI
	UNIFORM u_lightPosition		LIGHTDIRWORLD0
	UNIFORM u_eyePosition		EYEPOSWORLD

	UNIFORM u_ambient			MATERIALCOLORAMBIENT vec4(0.37,0.37,0.37,1.0)
	UNIFORM u_diffuse			MATERIALCOLORDIFFUSE vec4(0.88,0.88,0.88,1.0)
	UNIFORM u_specular			MATERIALCOLORSPECULAR vec4(0.5,0.5,0.5,1.0)
	UNIFORM u_specularPower		MATERIALSHININESS float(25.0)
	
	UNIFORM	s_baseMap			TEXTURE0
	UNIFORM	s_bumpMap			TEXTURE1
	UNIFORM s_envMap			TEXTURE2

	TEXTURE 0 baseMap
	TEXTURE 1 bumpMap
	TEXTURE 2 envMap
	
	FRAGMENTSHADER  FragShader
	VERTEXSHADER    VertShader
[/EFFECT]
