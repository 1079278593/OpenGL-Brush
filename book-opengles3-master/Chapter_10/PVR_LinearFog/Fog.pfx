[HEADER]
	VERSION		00.00.00.00
	DESCRIPTION Fog Effect Example
	COPYRIGHT	Imagination Technologies Ltd.
[/HEADER]

[TEXTURES]
	FILE base 	shaman_basemap.pvr		LINEAR-LINEAR-NONE
[/TEXTURES]

[VERTEXSHADER]
	NAME VertShader
	[GLSL_CODE]
#version 300
// Above should be "#version 300 es" but PVRShaman seems to have a bug and does not accept it
uniform mat4 u_matViewProjection;
uniform mat4 u_matView;
uniform vec4 u_eyePos;

in vec4 a_vertex;
in vec2 a_texCoord0;

out vec2 v_texCoord;
out float v_eyeDist;

void main( void )
{
    // Transform vertex to view-space
    vec4 vViewPos = u_matView * a_vertex;
    
    // Compute the distance to eye
    v_eyeDist = sqrt( (vViewPos.x - u_eyePos.x) *
                      (vViewPos.x - u_eyePos.x) +
                      (vViewPos.y - u_eyePos.y) *
                      (vViewPos.y - u_eyePos.y) +
                      (vViewPos.z - u_eyePos.z) *
                      (vViewPos.z - u_eyePos.z) );                      

    gl_Position = u_matViewProjection * a_vertex;
    v_texCoord  = a_texCoord0.xy;
}
	[/GLSL_CODE]
[/VERTEXSHADER]

[FRAGMENTSHADER]
	NAME FragShader
	[GLSL_CODE]
#version 300
// Above should be "#version 300 es" but PVRShaman seems to have a bug and does not accept it
precision mediump float;

uniform vec4 u_fogColor;
uniform float u_fogMaxDist;
uniform float u_fogMinDist;
uniform sampler2D baseMap;

in vec2 v_texCoord;
in float v_eyeDist;

layout( location = 0 ) out vec4 outColor;

float computeLinearFogFactor()
{
   float factor;
   
   // Compute linear fog equation
   factor = (u_fogMaxDist - v_eyeDist) /
            (u_fogMaxDist - u_fogMinDist );
   
   // Clamp in the [0,1] range
   factor = clamp( factor, 0.0, 1.0 );
            
   return factor;            
}

void main( void )
{
    float fogFactor = computeLinearFogFactor();
    vec4 baseColor = texture( baseMap, v_texCoord );
    
    // Compute final color as a lerp with fog factor
    outColor = baseColor * fogFactor + 
    		   u_fogColor * (1.0 - fogFactor); 
}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

[EFFECT]
	NAME Fog

	ATTRIBUTE	a_vertex			POSITION
	ATTRIBUTE	a_texCoord0			UV
	
	UNIFORM 	u_matViewProjection	WORLDVIEWPROJECTION
	UNIFORM		u_matView			WORLDVIEW
	
	UNIFORM		u_fogColor			FOGCOLOR	vec4(1.0,1.0,1.0,1.0)
	UNIFORM		u_fogMaxDist		FOGMAXDIST	float(320.0)
	UNIFORM		u_fogMinDist		FOGMINDIST	float(280.0)

	UNIFORM		baseMap				TEXTURE0

	TEXTURE 0 base

	VERTEXSHADER   VertShader
	FRAGMENTSHADER FragShader
[/EFFECT]